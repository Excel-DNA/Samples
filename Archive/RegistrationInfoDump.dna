<DnaLibrary RuntimeVersion="v4.0" Language="VisualBasic">
<Reference Name="Microsoft.Office.Interop.Excel" />
<![CDATA[

Imports Microsoft.Office.Interop.Excel

Public Module TestRegistrationInfo

    <ExcelCommand(MenuName:="Excel-DNA", MenuText:="Registration Info Dump")>
    Sub RegistrationInfo()
        Try
            Dim Application As Application = ExcelDnaUtil.Application
            Dim addinPaths As IEnumerable(Of String)

            If ExcelDnaUtil.ExcelVersion >= 14.0 Then
                addinPaths = From addIn As AddIn In Application.AddIns2
                             Where addIn.IsOpen
                             Select addIn.FullName
            Else
                Dim allPaths As New HashSet(Of String)
                Dim funcInfos As Object = Application.RegisteredFunctions

                If Not funcInfos Is Nothing Then
                    For i As Integer = LBound(funcInfos) to UBound(funcInfos)
                        allPaths.Add(funcInfos(i, 1))
                    Next
                End If
                addinPaths = allPaths
            End If

            Dim wb = Application.Workbooks.Add()
            Dim shIndex As Worksheet= wb.Sheets(1)
            shIndex.Name = "Add-Ins"
            shIndex.Cells(1, 1) = "Add-In Path"
            shIndex.Cells(1, 2) = "Registration Info?"

            Dim row As Integer = 2
            For Each path As String In addinPaths
                shIndex.Cells(row, 1) = path

                ' Try to read RegistrationInfo
                Dim result = ExcelIntegration.GetRegistrationInfo(path, 0)
                If result.Equals(ExcelError.ExcelErrorNA) Then
                    shIndex.Cells(row, 2) = False
                Else
                    shIndex.Cells(row, 2) = True

                    ' Dump the result to a new sheet
                    Dim shInfo As Worksheet = wb.Sheets.Add(After:=wb.Sheets(wb.Sheets.Count))
                    shInfo.Name = System.IO.Path.GetFileName(path)

                    ' C API via ExcelReference would work well here
                    Dim refInfo = New ExcelReference(0, UBound(result), 0, 254, shInfo.Name)
                    refInfo.SetValue(result)

                End If

                row = row + 1
            Next
            shIndex.Activate()

        Catch ex As Exception
            Debug.Print(ex.ToString())
        End Try
    End Sub

    End Module
]]>

</DnaLibrary>